

model {

### Priors definition ###
for(s in 1:3){
	for(p in 1:2){
		for(i in 1:2){
			beta[p,s,i] ~ dnorm(0,0.1)
		}
	
		for(i in 1:4){
			delta[p,s,i] ~ dnorm(0,0.1)
		}

		for(ss in 1:3){
			gamma_e[p,s,ss] ~ dnorm(0,0.1)
			gamma_c[p,s,ss] ~ dnorm(0,0.1)
		}
	} # End Period
	
	psi[s] ~ dunif(0,1)

	d[s] ~ dunif(0,1)

	alpha[s] ~ dunif(0.00001, 0.5)

} # End Species

### PARAMETERS DEFINITION ###

for(isl in 1:nislands){
	for(s in 1:3){
		for(i in 1:nsites[isl]){
	  	for(j in 1:nsites[isl]){
	    		dist[isl,s,i,j] = exp(-alpha[s] * distM[isl,i,j])
	  	}
		}

		scaling_param[isl,s] = sum(dist[isl,s,1:nsites[isl],1:nsites[isl]]) / ( nsites[isl] )

	for(p in 1:2){
	for(j in 1:nyears){
	for(i in 1:(nsites[isl])){
		log(e[isl,s,p,i,j]) <- beta[p,s,1] + delta[p,s,1] * cov[isl,i,1] + delta[p,s,2] * cov[isl,i,2] + (mask[s,] * gamma_e[p,s,]) %*% (occ[isl,,p,i,j])
		log(c[isl,s,p,i,j]) <- beta[p,s,2] + delta[p,s,3] * cov[isl,i,1] + delta[p,s,4] * cov[isl,i,2] + (mask[s,] * gamma_c[p,s,]) %*% (occ[isl,,p,i,j])
	}
	}
	}


	for(p in 1:2){
	  for(j in 1:(nyears)){
	    for(i in 1:(nsites[isl])){

	      x[isl,s,p,i,j] <- sum(dist[isl,s,1:nsites[isl],i] %*% occ[isl,s,p,1:nsites[isl],j]) / scaling_param[isl,s]

	      eventRate[isl,s,p,i,j] = ( 1-exp( - ( c[isl,s,p,i,j] * x[isl,s,p,i,j] + e[isl,s,p,i,j]) ) )
	      eventE[isl,s,p,i,j] = e[isl,s,p,i,j] / (c[isl,s,p,i,j] * x[isl,s,p,i,j] + e[isl,s,p,i,j])

	      transition[isl,s,p,i,j,2] <- 1 - ( eventRate[isl,s,p,i,j] * eventE[isl,s,p,i,j] ) 
	      transition[isl,s,p,i,j,1] <- eventRate[isl,s,p,i,j] * (1 - eventE[isl,s,p,i,j])

	    }
	  }
	}

}}
### State-Space model likelihood ###

for(isl in 1:nislands){
for(s in 1:3){
	 for(i in 1:nsites[isl]){
	    occ[isl,s,1,i,1] ~ dbern(psi[s])
	    obs[isl,s,1,i,1] ~ dbern(occ[isl,s,1,i,1] * X[isl,1,i,1] * d[s])
	  }

	  for(i in 1:nsites[isl]){

	    for( j in 1:nyears){
	      occ[isl,s,2,i,j] ~ dbern(transition[isl,s,1,i,j,occ[isl,s,1,i,j]+1])
	      obs[isl,s,2,i,j] ~ dbern(occ[isl,s,2,i,j] * X[isl,2,i,j] * d[s])
	    }
	    
	    for( j in 2:nyears){
	      occ[isl,s,1,i,j] ~ dbern(transition[isl,s,2,i,j-1,occ[isl,s,2,i,j-1]+1])
	      obs[isl,s,1,i,j] ~ dbern(occ[isl,s,1,i,j] * X[isl,1,i,j] * d[s])
	    }

	    for(p in 1:2){
	      for(j in 1:4){
		revisit[isl,s,p,i,j] ~ dbern(occ[isl,s,p,i,27+j] * X[isl,p,i,27+j] * d[s]) 
	      }
	    }
	  }
}
}
}
             

